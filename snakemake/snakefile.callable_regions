# vim: syntax=python

# Both summarizes a vector into a (2-dimensional) table and adds
# bulk depth information.
# The memory limit here can be insufficient if the size of each
# GATK chunk is large (>10MB or so).
rule callable_region_summarize_per_sample:
    input:
        "callable_regions/joint_coverage_matrix.tab.gz"
    output:
        "callable_regions/{sample}_coverage_table.rda",
    resources:
        mem=7000
    threads: 20
    script:
        "scripts/callable_regions_summarize_script.R"


rule callable_region_gather:
    input:
        expand("callable_regions/gatk_depthofcoverage/chunk{gatk_chunk}.txt",
            gatk_chunk=range(1, config['gatk_chunks']+1))
    output:
        tabgz="callable_regions/joint_coverage_matrix.tab.gz",
        tabidx="callable_regions/joint_coverage_matrix.tab.gz.tbi"
    resources:
        mem=1000
    shell:
        """
        (grep '^#' {input[0]} ; cat {input} | grep -v '^#') \
            | {config[scripts]}/totab.callable_regions.sh /dev/stdin /dev/stdout \
            | bgzip -c > {output.tabgz}
        tabix -p vcf -S 1 {output.tabgz}
        """


# Needs to match parallelization of GATK scattering, else the
# callable regions will not match the actual genotyped area.
# Also needs to match GATK filtering (mapping quality, additional
# default HaplotypeCaller read filters) to be meaningful.
rule callable_region_scatter:
    input:
        scbam=config['bam_map'].values()
    output:
        tmp="callable_regions/gatk_depthofcoverage/chunk{gatk_chunk}.txt"
    params:
        bams=" ".join([ "-I " + bam for bam in config['bam_map'].values() ]),
        regionflag=lambda wildcards:
            "-L " + config['gatk_regions'][int(wildcards.gatk_chunk) - 1],
    benchmark:
        "callable_regions/gatk_depthofcoverage/benchmark_depthofcoverage_chunk{gatk_chunk}.txt"
    resources:
        mem=6000
    shell:
        """
        gatk DepthOfCoverage \
            --java-options '-Xmx5G -Xms5G' \
            -R {config[ref]} \
            {params.bams} \
            {params.regionflag} \
            --minimum-mapping-quality 60 \
            --read-filter NotSecondaryAlignmentReadFilter \
            --read-filter GoodCigarReadFilter \
            --read-filter NonZeroReferenceLengthAlignmentReadFilter \
            --read-filter PassesVendorQualityCheckReadFilter \
            --read-filter MappedReadFilter \
            --read-filter MappingQualityAvailableReadFilter \
            --read-filter NotDuplicateReadFilter \
            --read-filter MappingQualityReadFilter \
            --read-filter WellformedReadFilter \
            --omit-interval-statistics true \
            --omit-locus-table true \
            --omit-per-sample-statistics true \
            --output-format TABLE \
            -O {output.tmp}
        """
