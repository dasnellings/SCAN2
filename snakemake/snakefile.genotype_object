# vim: syntax=python

rule pregenotype:
    input:
        inttab="call_mutations/integrated_table.tab.gz",
        sccigars="call_mutations/{sample}/cigars.tab.gz",
        bulkcigars="call_mutations/%s/cigars.tab.gz" % config['bulk_sample'],
        config="scan.yaml"
    output:
        cigardata=temp("call_mutations/{sample}/cigardata.tab"),
        cigardatagz="call_mutations/{sample}/cigardata.tab.gz",
        cigardataidx="call_mutations/{sample}/cigardata.tab.gz.tbi",
        fdrpriordata="call_mutations/{sample}/fdr_prior_data.rda"
    threads: 1
    params:
        sc_sample="{sample}",
        bulk_sample=config['bulk_sample']
    resources:
        mem=20000
    benchmark:
        "call_mutations/{sample}/benchmark_pregenotyping.txt"
    shell:
        """
        {config[scripts]}/pregenotyping.R \
            {params.sc_sample} {params.bulk_sample} {input.inttab} \
            {input.sccigars} {input.bulkcigars} {input.config} \
            {output.cigardata} {output.fdrpriordata}
        """


rule genotype:
    input:
        inttab="call_mutations/integrated_table.tab.gz",
        fits="ab_model/{sample}/fits.rda",
        sccigars="call_mutations/{sample}/cigars.tab.gz",
        bulkcigars="call_mutations/%s/cigars.tab.gz" % config['bulk_sample'],
        cigardata="call_mutations/{sample}/cigardata.tab.gz",
        fdrpriordata="call_mutations/{sample}/fdr_prior_data.rda"
    output:
        "call_mutations/{sample}/scan2_object.rda"
    params:
        sc_sample="{sample}",
        bulk_sample=config['bulk_sample'],
        genome=config['genome']
    threads: 20
    resources:
        mem=32000
    benchmark:
        "call_mutations/{sample}/benchmark_genotype.txt"
    shell:
        """
        {config[scripts]}/run_pipeline.R \
           {params.sc_sample} {params.bulk_sample} \
           {input.inttab} {input.fits} \
           {input.sccigars} {input.bulkcigars} \
           {input.cigardata} {input.fdrpriordata} {params.genome} \
           {output}
        """
