# vim: syntax=python
configfile: "scan.yaml"

samples = config['sc_bams'].keys()

rule all: 
    input: 
        expand("call_mutations/{sample}/scan2_object_update.rda", sample=samples)       

rule update_table:
    input:
        inttab="call_mutations/integrated_table.tab.gz",
        panel=config["panel"],
    output:
        outtab="call_mutations/integrated_table_update.tab",
        outtabgz="call_mutations/integrated_table_update.tab.gz",
        outtabidx="call_mutations/integrated_table_update.tab.gz.tbi", 
        details="call_mutations/germline_resampling_details_update.rda",
    log:
        "call_mutations/integrate_tables_update.log"
    params:
        genome=config['genome'],
        config_yaml='scan.yaml',    # quite the hack. would really prefer not to do this
    threads: config['integrate_table_n_cores']
    resources:
        mem=2000*config['integrate_table_n_cores']
    benchmark:
        "call_mutations/benchmark_integrate_tables_update.txt"
    shell:
        """
        {config[scripts]}/panel_filter.R {input.inttab} {output.outtab} \
		{input.panel} {params.genome} {threads} >& {log}
        """
 
rule pregenotype:
    input:
        inttab="call_mutations/integrated_table_update.tab.gz",
        inttabidx="call_mutations/integrated_table_update.tab.gz.tbi",
        sccigars="call_mutations/{sample}/cigars.tab.gz",
        sccigarsidx="call_mutations/{sample}/cigars.tab.gz.tbi",
        bulkcigars="call_mutations/%s/cigars.tab.gz" % config['bulk_sample'],
    output:
        cigardata=temp("call_mutations/{sample}/cigardata_update.tab"),
        cigardatagz="call_mutations/{sample}/cigardata_update.tab.gz",
        cigardataidx="call_mutations/{sample}/cigardata_update.tab.gz.tbi",
    log:
        "call_mutations/{sample}/call_mutations_update.log"
    threads: 1
    params:
        sc_sample="{sample}",
        bulk_sample=config['bulk_sample'],
        genome=config['genome']
    resources:
        mem=8000
    benchmark:
        "call_mutations/{sample}/benchmark_pregenotyping_update.txt"
    shell:
        """
        {config[scripts]}/pregenotyping.R \
            {params.sc_sample} {params.bulk_sample} {input.inttab} \
            {input.sccigars} {input.bulkcigars} {params.genome} \
            {output.cigardata} >& {log}
        """


rule genotype:
    input:
        inttab="call_mutations/integrated_table_update.tab.gz",
        inttabidx="call_mutations/integrated_table_update.tab.gz.tbi",
        fits="ab_model/{sample}/fits.rda",
        sccigars="call_mutations/{sample}/cigars.tab.gz",
        sccigarsidx="call_mutations/{sample}/cigars.tab.gz.tbi",
        bulkcigars="call_mutations/%s/cigars.tab.gz" % config['bulk_sample'],
        bulkcigarsidx="call_mutations/%s/cigars.tab.gz.tbi" % config['bulk_sample'],
        cigardata="call_mutations/{sample}/cigardata_update.tab.gz",
        cigardataidx="call_mutations/{sample}/cigardata_update.tab.gz.tbi",
        dptab="depth_profile/{sample}_depth_table.rda"
    output:
        "call_mutations/{sample}/scan2_object_update.rda"
    log:
        "call_mutations/{sample}/call_mutations_update.log"
    params:
        sc_sample="{sample}",
        # bit of a hack here: assumes the script is run from the root of the
        # working directory (i.e., the same as is assumed for input/output
        # file names).
        config_yaml='scan.yaml'
    threads: config['genotype_n_cores']
    resources:
        mem=1500*config['genotype_n_cores']
    benchmark:
        "call_mutations/{sample}/benchmark_genotype_update.txt"
    shell:
        """
        {config[scripts]}/call_mutations.R \
           {params.sc_sample} {params.config_yaml} \
           {input.inttab} {input.fits} \
           {input.sccigars} {input.bulkcigars} \
           {input.cigardata} {input.dptab} \
           {output} {threads} >& {log}
        """


